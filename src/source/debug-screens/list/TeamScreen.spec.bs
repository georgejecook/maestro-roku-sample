import "pkg:/source/tests/BaseTestSuite.spec.bs"
import "pkg:/source/debug-screens/list/TeamScreen.bs"

namespace tests
  @suite("TeamScreen tests")
  class TeamScreenTests extends tests.BaseTestSuite
    private screen as mc.types.node

    protected override function beforeEach()
      super.beforeEach()
      m.screen = m.createNodeClass(ml.sample.TeamScreen)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    @describe("initialize ")
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    @it("it creates the views")
    function _()
      rectangle = { "id": "rectangle" }
      listHolder = { "id": "listHolder" }
      list = { "id": "list" }

      m.screen.list = list
      m.screen.listHolder = listHolder

      m.expectCalled(m.screen.createSGNode("mv_Rectangle", m.screen.top, "bgRect", {
        translation: [0, 0]
        size: [1920, 1080]
        color: "#6a307d"
      }), rectangle)

      m.expectCalled(m.screen.createSGNode("Group", m.screen.top, "listHolder"), listHolder)
      m.expectCalled(m.screen.createSGNode("ml_List", m.screen.listHolder, "list", {
        translation: [0, 0]
        width: 1920
        height: 1080
      }), list)

      m.expectCalled(m.screen.setInstance("list", m.screen.list))

      m.expectCalled(m.screen.createSGNode("LoadingIndicator", m.screen.top, "loadingIndicator", {
        isActive: true
        width: 1920
        height: 1080
        backgroundVisible: false
      }))

      m.expectCalled(m.screen.onResetChange())

      m.screen.initialize()

    end function
  end class
end namespace