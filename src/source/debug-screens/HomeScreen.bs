import "pkg:/source/roku_modules/maestro/view/BaseScreen.brs"
import "pkg:/source/roku_modules/maestro/core/Utils.brs"
import "pkg:/source/roku_modules/maestro/core/DebugTimer.brs"
import "pkg:/source/roku_modules/maestro/ml/ListMixin.brs"


@node("HomeScreen", "mv_BaseScreen")
class HomeScreen extends mv.BaseScreen

  'views
  private list
  private listHolder
  private bgRect

  'style
  private numberOfRows = 10
  private defaultCellType = "CustomSimpleCell"

  'state
  private contentId = 0
  private rowSettings
  private focusSettings
  private cellSettings
  private loadingIndicator

  @inject("appController")
  private appController

  function new()
    super("HomeScreen")
  end function

  protected override function initialize()
    super.initialize()
    size = [1920, 1080]

    m.bgRect = m.createSGNode("mv_Rectangle", m.top, "bgRect", {
      translation: [0, 0]
      size: size
      color: "#bebebe"
    })
    m.listHolder = m.createSGNode("Group", m.top, "listHolder")

    m.list = m.createSGNode("ml_List", m.listHolder, "list", {
      translation: [50, 200]
      width: size[0]
      height: size[1]
    })
    mioc.setInstance("list", m.list)
    m.loadingIndicator = m.createSGNode("LoadingIndicator", m.top, "loadingIndicator", {
      isActive: true
      width: 1920
      height: 1080
      backgroundVisible: false
    })
    m.onResetChange()
  end function

  private function onResetChange()
    m.list@.setListContent(invalid)

    m.cellSettings = ml.listMixin.createCellSettings("CustomSimpleCell", [560, 250], 5)

    m.focusSettings = ml.listMixin.createFocusSettings("fixed")
    m.focusSettings.feedbackStyle = "none"

    headerSettings = ml.listMixin.createHeaderSettings([10, 30], 80, "mediumBold,30", "#000000")
    m.rowSettings = ml.listMixin.createRowSettings(m.cellSettings, m.focusSettings, 10, headerSettings, -1, "ml_HorizontalRow", 250)
    m.rowSettings.numberItemsToRenderBefore = 10
    m.rowSettings.numberItemsToRenderAfter = 10
    m.rowSettings.screenPos = 300

    listSettings = m.createListSettings()
    content = ml.listMixin.createListContent(m.focusSettings, m.rowSettings, listSettings)

    timer = new mc.utils.DebugTimer("list")
    timer.log("makeRows")
    items = m.makeRows()

    m.setFocus(m.loadingIndicator)

    content@.setNotificationsEnabled(false)
    content@.appendChildren(items)

    m.list@.setContent(content)
    m.list.delegate = m.top

    timer.log("set content")
    if m.list.state = "rendered"
      m.onListRenderState("rendered")
      m.loadingIndicator.isActive = false
    else
      m.observeNodeField(m.list, "state", m.onListRenderState, "value", true)
    end if
  end function

  private function createListSettings()
    listSettings = ml.listMixin.createListSettings()
    listSettings.rowCreationStrategy = "async"
    listSettings.rowCreationBatchSize = 10
    listSettings.rowCreationBatchDelay = 0.3
    listSettings.numberItemsToRenderAfter = 10
    listSettings.numberItemsToRenderBefore = 10
    listSettings.renderStrategy = "dynamic"
    return listSettings
  end function

  private function onListRenderState(state as string)
    m.log.info("onListRenderState", state)
    if state = "rendered"
      m.loadingIndicator.isActive = false
    end if
  end function

  private function makeRows()
    content = []
    content.push(m.makeHeroRow())

    for i = 0 to m.numberOfRows - 1
      row = createObject("roSGNode", "ml_RowContent")
      row.settings = m.rowSettings
      row.id = rnd(2147483647).toStr()
      numItems = 20

      row.title = "Row " + str(i)
      content.push(row)
      row@.appendChildren(m.makeItems(numItems))
    end for
    return content
  end function

  private function makeItems(number as integer, size = [560, 250] as mc.types.array)
    items = []
    for i = 0 to number - 1
      items.push(m.makeItem(`Item ${i}`, size))
    end for
    return items
  end function

  private function makeItem(title as string, size = [560, 250] as mc.types.array) as mc.types.node
    id = rnd(2147483647).toStr()
    child = m.createSGNode("Item", invalid, id, {
      json: {
        title: title
        description: "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Cras aliquam lorem id ante feugiat fermentum. Proin mi erat, pretium at ante at, condimentum porta lacus. Proin hendrerit tincidunt risus blandit gravida. Duis imperdiet, nulla ut maximus viverra, magna nulla tristique diam, eget rhoncus eros sem quis magna. Quisque viverra ultricies sem ac dictum. Cras sit amet lectus eros. Duis lobortis ligula ac sapien pharetra imperdiet. Vestibulum dictum urna quis orci fermentum ultricies ac in orci."
        posterUrl: "https://picsum.photos/id/" + str(m.contentId mod 1000).trim() + `/${size[0]}/${size[1]}`

      }
    })
    child.HDGRIDPOSTERURL = "https://picsum.photos/id/" + str(m.contentId mod 1000).trim() + `/${size[0]}/${size[1]}`
    m.contentId++
    return child
  end function

  private function makeHeroRow()
    row = createObject("roSGNode", "ml_RowContent")
    row.id = "heroRow"

    cellSettings = ml.listMixin.createCellSettings("HeroCellEfficient", [1100, 700], 5)

    focusSettings = ml.listMixin.createFocusSettings("fixed")
    focusSettings.feedbackStyle = "none"

    rowSettings = ml.listMixin.createRowSettings(cellSettings, focusSettings, 10, invalid, -1, "ml_HorizontalRow", 700)
    rowSettings.numberItemsToRenderBefore = 4
    rowSettings.numberItemsToRenderAfter = 4

    row.settings = rowSettings

    row@.appendChildren(m.makeItems(6, [1100, 700]))
    return row
  end function


  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ lifecycle
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  protected override function onShow()
    m.log.info("onShow")
    super.onShow()
    m.appController@.setTopMenuVisible(true)
  end function

  protected override function onHide()
    super.onHide()
    m.list.isHidden = true
  end function

  protected override function onGainedFocus(isSelfFocused as boolean)
    super.onGainedFocus(isSelfFocused)
    m.log.info("onGainedFocus", isSelfFocused)

    if isSelfFocused
      m.setFocus(m.list)
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ list delegate
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function onCellWillGainFocus(event as ml.ListEvent)
  end function

  private function onCellDidGainFocus(event as ml.ListEvent)
  end function


  private function onCellDidLoseFocus(event as ml.ListEvent)
  end function

  private function onRowDidGainFocus(event as ml.ListEvent)

    row = m.list@.getRowRenderer(event.rowIndex)
    if row <> invalid
      mioc.setInstance("focusedIndex", {
        row: event.rowIndex
        cell: row.focusedCellIndex
      })
    else
      mioc.setInstance("focusedIndex", {
        row: -1
        cell: -1
      })

    end if
  end function

  private function onRowDidLoseFocus(event as ml.ListEvent)
  end function

  private function onRowGainingFocus(event as ml.ListEvent)
  end function

  private function onRowLosingFocus(event as ml.ListEvent)
  end function

  private function onCellGainingFocus(event as ml.ListEvent)
  end function

  private function onCellLosingFocus(event as ml.ListEvent)
  end function

  private function onItemSelected(event as ml.ListEvent)
    m.log.info("onItemSelected", event.rowIndex, event.cellIndex)

    item = m.list@.getItem(event.rowIndex, event.cellIndex)

    m.log.info("Selected Item", mc.dv(item))
    selection = m.createSelection(item, "display", "detailsScreen")

    m.executeSelection(selection)
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ Key Presses
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  private function isCapturingAnyKeyPress(key as string, press as boolean) as boolean
    return key <> "back" and key <> "up"
  end function
end class
